//file:noinspection GroovyAssignabilityCheck

plugins {
    id("java-library")
    id("idea")
    id("maven-publish")
    id("net.neoforged.gradle.userdev") version "$neoforge_gradle_version"
    id("org.jetbrains.kotlin.jvm") version "$kotlin_version"
    id("io.ktor.plugin") version "$ktor_version"
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }

    maven { url "https://maven.shedaniel.me/" }

    exclusiveContent {
        forRepository {
            maven {
                url = "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }

    maven {
        url "https://maven.squiddev.cc"
        content {
            includeGroup("cc.tweaked")
        }
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

minecraft.accessTransformers.file rootProject.file("src/main/resources/META-INF/accesstransformer.cfg")

configurations {
    kotlinLib {
        exclude(group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk7")
        exclude(group: "org.jetbrains.kotlin", module: "kotlin-stdlib-jdk8")
        exclude(group: "org.jetbrains.kotlin", module: "kotlin-stdlib")
        exclude(group: "org.jetbrains.kotlin", module: "kotlin-reflect")
        exclude(group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-core-jvm")
        exclude(group: "org.jetbrains.kotlinx", module: "kotlinx-coroutines-jdk8")
        exclude(group: "io.netty", module: "netty-codec-http")
    }
}

dependencies {
    implementation("net.neoforged:neoforge:$neo_version")
    implementation("thedarkcolour:kotlinforforge-neoforge:$kff_version")

    // Ktor
    kotlinLib("io.ktor:ktor-server-core-jvm:$ktor_version")
    kotlinLib("io.ktor:ktor-server-websockets-jvm:$ktor_version")
    kotlinLib("io.ktor:ktor-server-netty-jvm:$ktor_version")

    configurations.kotlinLib.allDependencies.each { dependency ->
        dependencies.add("implementation", dependency)
    }

    // TheOneProbe
    implementation("curse.maven:theoneprobe-245211:$top_version_id")

    // JourneyMap
    implementation("curse.maven:journeymap-32274:$journeymap_version_id")

    // REI
    api("dev.architectury:architectury-neoforge:$architectury_version")
    api("me.shedaniel.cloth:cloth-config-neoforge:$cloth_config_version")
    api("me.shedaniel:RoughlyEnoughItems-neoforge:$rei_version")

    // CC:Tweaked
    compileOnly("cc.tweaked:cc-tweaked-$minecraft_version-core-api:$cct_version")
    compileOnly("cc.tweaked:cc-tweaked-$minecraft_version-forge-api:$cct_version")
    runtimeOnly("cc.tweaked:cc-tweaked-$minecraft_version-forge:$cct_version")
}

subsystems {
    parchment {
        minecraftVersion = "$parchment_minecraft_version"
        mappingsVersion = "$parchment_version"
    }
}

runs {
    configureEach {
        // Recommended logging data for a userdev environment
        // The markers can be added/remove as needed separated by commas.
        // "SCAN": For mods scan.
        // "REGISTRIES": For firing of registry events.
        // "REGISTRYDUMP": For getting the contents of all registries.
        systemProperty("forge.logging.markers", "REGISTRIES")

        systemProperty("forge.logging.console.level", "debug")

        modSource(project.sourceSets.main)

        dependencies {
            project.configurations.kotlinLib.resolve().each {
                println(it)
            }
            runtime(project.configurations.kotlinLib)
        }
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
    }

    server {
        systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
        programArgument("--nogui")
    }

    // This run config launches GameTestServer and runs all registered gametests, then exits.
    // By default, the server will crash when no gametests are provided.
    // The gametest system is also enabled by default for other run configs under the /test command.
    gameTestServer {
        systemProperty("forge.enabledGameTestNamespaces", project.mod_id)
    }

    data {
        // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
        // workingDirectory project.file("run-data")

        // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
        programArguments.addAll("--mod", project.mod_id, "--all", "--output", file("src/generated/resources/").getAbsolutePath(), "--existing", file("src/main/resources/").getAbsolutePath())
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir("src/generated/resources") }

// Sets up a dependency configuration called "localRuntime".
// This configuration should be used instead of "runtimeOnly" to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom(localRuntime)
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            kff_version            : kff_version,
            kff_version_range      : kff_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_version            : mod_version
    ]
    inputs.properties(replaceProperties)

    filesMatching(["META-INF/neoforge.mods.toml", "*.mixins.json"]) {
        expand(replaceProperties)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

tasks.named("wrapper", Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}
